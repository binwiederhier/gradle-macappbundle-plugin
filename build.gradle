buildscript {
    repositories {
        jcenter()
        mavenCentral()
    }
    dependencies {
        classpath 'org.codehaus.groovy:groovy-all:2.3.6'
        classpath 'nu.studer:gradle-plugindev-plugin:1.0.3'
    }
}

apply plugin: 'cpp'
apply plugin: 'objective-c'
apply plugin: 'groovy'
apply plugin: 'nu.studer.plugindev'

group = 'com.github.cr0'

model {
    platforms {
        osx {
            operatingSystem "osx"
        }
    }

    buildTypes {
        release
    }
}

executables {
    main {
        baseName "JavaAppLauncher"
    }

    all {
        binaries.all {
            if (targetPlatform.operatingSystem.macOsX) {
                objcCompiler.args '-I', "${org.gradle.internal.jvm.Jvm.current().javaHome}/include"
                objcCompiler.args '-I', "${org.gradle.internal.jvm.Jvm.current().javaHome}/include/darwin"
                objcCompiler.args '-F', "${org.gradle.internal.jvm.Jvm.current().javaHome}/../../"
                linker.args '-framework', "Cocoa"
                linker.args '-isysroot', "/Applications/Xcode.app/Contents/Developer/Platforms/MacOSX.platform/Developer/SDKs/MacOSX10.10.sdk"
            }
        }
    }
}

copy {
    from 'build/binaries/mainExecutable/JavaAppLauncher'
    into 'src/main/resources/com/github/cr0/macAppBundle'
}

plugindev {
    pluginId 'com.github.cr0.macAppBundle'
    pluginImplementationClass 'com.github.cr0.gradle.macAppBundle.MacAppBundlePlugin'
    pluginDescription 'A Gradle Plugin to create a Mac OSX .app application based on the project with customizable class paths.'
    pluginLicenses 'Apache-2.0'
    pluginTags 'gradle', 'osx', 'appbundle'
    authorId 'cr0'
    authorName 'Christian Roth'
    authorEmail 'christian.roth@port17.de'
    projectUrl 'https://github.com/cr0/gradle-macappbundle'
    projectInceptionYear '2015'
    done() // do not omit this
}


